plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField('boolean', 'IS_TEST', project.IS_TEST_ENV)
        buildConfigField('String', 'APP_VERSION_NAME', "\"${project.APP_VERSION_NAME}\"")

        ensureProperty('FEED_SHARE_CONTENT_PARTNER')
        buildConfigField('String', 'FEED_SHARE_CONTENT_PARTNER', "\"${project.FEED_SHARE_CONTENT_PARTNER}\"")

        ensureProperty('FEED_SHARE_CONTENT_PARTNER')
        buildConfigField('String', 'FEED_SHARE_CONTENT_CATEGORY', "\"${project.FEED_SHARE_CONTENT_CATEGORY}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs', project(':scene-core').files('libs')
    }
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "androidx.appcompat:appcompat:$AppCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$RecyclerViewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$ConstraintLayoutVersion"
    implementation "com.google.android.material:material:$MaterialVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.10"

    implementation('com.bytedance.ugc.framework.libs:ttsdk-ttlivepull:1.25.2.106') {
        exclude group: 'com.bytedance.ugc.framework.libs', module: 'ttsdk-ttrtcbase'
    }
    implementation('com.bytedance.ugc.framework.libs:ttsdk-player_premium:1.25.2.106') {
        exclude group: 'com.bytedance.ugc.framework.libs', module: 'ttsdk-ttrtcbase'
    }

    implementation project(":scene-core")

    implementation "org.greenrobot:eventbus:$EventBusVersion"
    implementation "com.google.code.gson:gson:$GsonVersion"
    implementation "com.github.bumptech.glide:glide:$GlideVersion"
}

private def ensureProperty(String propertyName) {
    if (!project.hasProperty(propertyName) || project[propertyName].isEmpty()) {
        throw new GradleException("NO ${propertyName} defined in gradle.properties!")
    }
}